buildscript {
    ext {
        kotlin_version = "1.4.30"
        kotlin_coroutines_version = "1.3.9"
        lifecycle_version = "2.2.0"
        okhttp_version = "4.9.0"
        play_services_ads = "19.7.0"
        play_services_location = "17.1.0"
        retrofit_version = "2.9.0"
        worker_version = "2.5.0"
        room_version = "2.2.6"
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jacoco:org.jacoco.core:0.8.6"
    }
}

subprojects {
    repositories {
        google()
        jcenter()
    }

    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply from: '../jacoco.gradle'
    apply from: '../publish.gradle'

    android {
        compileSdkVersion 30
        buildToolsVersion "30.0.3"

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 30
            versionCode 1
            versionName "${project.version}"
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            consumerProguardFiles "consumer-rules.pro"

            buildConfigField "String", "SDK_VERSION", "\"${project.version}\""
            buildConfigField "String", "PRE_PROD_CLIENT_ID", findApiKey("PRE_PROD_CLIENT_ID")
            buildConfigField "String", "PRE_PROD_CLIENT_SECRET", findApiKey("PRE_PROD_CLIENT_SECRET")
            buildConfigField "String", "PRE_PROD_REDIRECT_URI", findApiKey("PRE_PROD_REDIRECT_URI")
            buildConfigField "String", "PROD_CLIENT_ID", findApiKey("PROD_CLIENT_ID")
            buildConfigField "String", "PROD_CLIENT_SECRET", findApiKey("PROD_CLIENT_SECRET")
            buildConfigField "String", "PROD_REDIRECT_URI", findApiKey("PROD_REDIRECT_URI")
        }

        sourceSets {
            main {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java.srcDirs = ['src/main/java']
                res.srcDirs = ['src/main/res']
            }
        }

        buildTypes {
            debug {
                testCoverageEnabled true
            }
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        kotlinOptions {
            jvmTarget = '1.8'
        }
        testOptions {
            unitTests {
                includeAndroidResources = true
            }
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
        implementation "com.google.android.gms:play-services-location:$play_services_location"

        // Retrofit for API call
        implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
        implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
        implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"

        // Logging Network Calls
        implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

        // Kotlin coroutines
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
        testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2"

        implementation "com.jakewharton.threetenabp:threetenabp:1.3.0"

        // Unit tests
        testImplementation "junit:junit:4.13.1"
        testImplementation 'org.hamcrest:hamcrest-library:2.2'
        testImplementation "org.robolectric:robolectric:4.4"
        testImplementation "org.mockito:mockito-core:3.6.0"
        testImplementation "androidx.test:core:1.3.0"
        testImplementation 'org.mockito:mockito-inline:2.13.0'

        // Room
        implementation "androidx.room:room-runtime:$room_version"
        kapt "androidx.room:room-compiler:$room_version"
        testImplementation "androidx.room:room-testing:$room_version"
    }
}

static def findApiKey(String property){
    Properties properties = new Properties()
    properties.load(new FileInputStream("platform-secrets.properties"))
    return "\"" + properties.getProperty(property) +"\""
}

task clean(type: Delete) {
    delete rootProject.buildDir
}